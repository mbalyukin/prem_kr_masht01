
#Область СозданиеСкладскихДокументов

&ИзменениеИКонтроль("ЗаписатьОбъектОрдер")
Функция прем_ЗаписатьОбъектОрдер(ДокументОбъект, ФоновоеЗадание)
	ЕстьОшибка = Ложь;
	ДокументОбъект.ТоварыПоРаспоряжениям.Свернуть("Распоряжение, Номенклатура, Характеристика, Назначение, Серия", "Количество");
	ДокументОбъект.Заполнить(Неопределено);
#Вставка
	Если Не ДокументОбъект.ЭтоНовый() Тогда
		ТаблицаСвязанныхТТН = прем_ТаблицаСвязанныхТТН(ДокументОбъект.Ссылка);
		Если ТаблицаСвязанныхТТН.Количество() = 0 Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Запрещена модификация ордера, не введены ТТН.'"),,,, ЕстьОшибка);
		КонецЕсли;
	КонецЕсли;
#КонецВставки
	Попытка
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);				
	Исключение
		Если ФоновоеЗадание Тогда
			ТекстСообщения = НСтр("ru = 'Не удалось записать расходный ордер фоновым заданием по причине: %Причина%'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", ДокументОбъект.Ссылка);
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.РасходныйОрдерНаТовары,
				ДокументОбъект.Ссылка,
				ТекстСообщения);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецЕсли;	
			
		ЕстьОшибка = Истина;
	КонецПопытки;
	
	Возврат Не ЕстьОшибка;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция прем_ТаблицаСвязанныхТТН(ПриходныйОрдерНаТовары)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТТНВходящаяЕГАИС.Ссылка КАК ТТНВходящаяЕГАИС,
	|	ТТНВходящаяЕГАИС.Номер КАК Номер,
	|	ТТНВходящаяЕГАИС.Дата КАК Дата,
	|	ТТНВходящаяЕГАИС.Проведен КАК Проведен,
	|	СтатусыДокументовЕГАИС.Статус КАК Статус
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС КАК ТТНВходящаяЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО (ТТНВходящаяЕГАИС.Ссылка = (ВЫРАЗИТЬ(СтатусыДокументовЕГАИС.Документ КАК Документ.ТТНВходящаяЕГАИС)))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриходныйОрдерНаТовары КАК ПриходныйОрдерНаТовары
	|		ПО ((ВЫРАЗИТЬ(ТТНВходящаяЕГАИС.ДокументОснование КАК Документ.ПриобретениеТоваровУслуг)) = (ВЫРАЗИТЬ(ПриходныйОрдерНаТовары.Распоряжение КАК Документ.ПриобретениеТоваровУслуг)))
	|			И (ПриходныйОрдерНаТовары.Ссылка = &ПриходныйОрдерНаТовары)";
	
	Запрос.УстановитьПараметр("ПриходныйОрдерНаТовары", ПриходныйОрдерНаТовары);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти
