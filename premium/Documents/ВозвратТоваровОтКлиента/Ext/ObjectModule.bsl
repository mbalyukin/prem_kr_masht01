
#Область ОбработчикиСобытийДокумента

&После("ОбработкаПроведения")
Процедура прем_ОбработкаПроведения(Отказ, РежимПроведения)
	
	прем_ПроверитьНаличиеАктивнойТТН(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура прем_ПроверитьНаличиеАктивнойТТН(Отказ)
	
	Если Не прем_ЭтоОперацияПоАлкоголю() Тогда
		Возврат;
	КонецЕсли;
	
	ТТНВходящая = прем_СвязаннаяТТНВходящая();
	Если ТТНВходящая = Неопределено Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Запрещено проведение документа, нет активной ТТН.'"),,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Функция прем_ЭтоОперацияПоАлкоголю()
	
	Возврат Ложь;
	
КонецФункции

Функция прем_СвязаннаяТТНВходящая()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТТНВходящаяЕГАИС.Ссылка КАК ТТНВходящаяЕГАИС,
	|	ТТНВходящаяЕГАИС.Номер КАК Номер,
	|	ТТНВходящаяЕГАИС.Дата КАК Дата,
	|	ТТНВходящаяЕГАИС.Проведен КАК Проведен,
	|	СтатусыДокументовЕГАИС.Статус КАК Статус
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС КАК ТТНВходящаяЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО (ТТНВходящаяЕГАИС.Ссылка = (ВЫРАЗИТЬ(СтатусыДокументовЕГАИС.Документ КАК Документ.ТТНВходящаяЕГАИС)))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
	|		ПО ((ВЫРАЗИТЬ(ТТНВходящаяЕГАИС.ДокументОснование КАК Документ.ПриобретениеТоваровУслуг)) = ПриобретениеТоваровУслуг.Ссылка)
	|			И (ПриобретениеТоваровУслуг.Ссылка = &ПриобретениеТоваровУслуг)
	|ГДЕ
	|	НЕ ВЫРАЗИТЬ(СтатусыДокументовЕГАИС.Статус КАК Перечисление.СтатусыОбработкиТТНВходящейЕГАИС) В (&СтатусыОтмены)
	|	И ТТНВходящаяЕГАИС.Проведен";
	
	Запрос.УстановитьПараметр("ПриобретениеТоваровУслуг", Ссылка);
	
	СтатусыОтмены = Новый Массив;
	СтатусыОтмены.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.Отменен);
	СтатусыОтмены.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.ОтмененПоставщиком);
	
	Запрос.УстановитьПараметр("СтатусыОтмены", СтатусыОтмены);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Возврат Выборка;
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти
