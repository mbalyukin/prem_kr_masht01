
#Область ПрограммныйИнтерфейс

// Возвращает данные всех ТТН входящих, связанных с документом.
//
// Параметры:
//  Документ - ДокументСсылка - связанный с ТТН документ.
//  ТолькоПроведенные - Булево
//  НеОтмененные - Булево
//
// Возвращаемое значение:
//   ТаблицаЗначений
//
Функция прем_ДанныеТТНВходящихПоСвязанномуДокументу(Документ, ТолькоПроведенные = Ложь, НеОтмененные = Ложь) Экспорт
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	ТекстЗапросаТТНПоСвязанномуДокументу = прем_ТекстЗапросаТТНПоСвязанномуДокументу(Документ);
	Если ТекстЗапросаТТНПоСвязанномуДокументу = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекстЗапросаДанныхТТН = прем_ТекстЗапросаДанныхТТН(НеОтмененные);
	
	ЧастиЗапроса = Новый Массив;
	ЧастиЗапроса.Добавить(ТекстЗапросаТТНПоСвязанномуДокументу);
	ЧастиЗапроса.Добавить(ТекстЗапросаДанныхТТН);
	
	Запрос.Текст = СтрСоединить(ЧастиЗапроса, ";" + Символы.ПС);
	
	Запрос.УстановитьПараметр("СвязанныйДокумент", Документ);
	
	ДанныеТТНВходящих = Запрос.Выполнить().Выгрузить();
	Если ТолькоПроведенные Тогда
		Возврат ДанныеТТНВходящих.Скопировать(ДанныеТТНВходящих.НайтиСтроки(Новый Структура("Проведен", Истина)));
	Иначе
		Возврат ДанныеТТНВходящих;
	КонецЕсли;
	
КонецФункции

// Возвращает данные ТТН входящей - частный случай, когда ТТН введена только одна.
//
// Параметры:
//  Документ - ДокументСсылка - связанный с ТТН документ.
//  ТолькоПроведенная - Булево
//  НеОтмененная - Булево
//
// Возвращаемое значение:
//   Структура
//
Функция прем_ДанныеТТНВходящейПоСвязанномуДокументу(Документ, ТолькоПроведенная = Ложь, НеОтмененная = Ложь) Экспорт
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеТТНВходящихПоСвязанномуДокументу = прем_ДанныеТТНВходящихПоСвязанномуДокументу(Документ, ТолькоПроведенная, НеОтмененная);
	
	Если ДанныеТТНВходящихПоСвязанномуДокументу <> Неопределено
		И ДанныеТТНВходящихПоСвязанномуДокументу.Количество() > 0 Тогда
		
		Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(ДанныеТТНВходящихПоСвязанномуДокументу)[0];
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПолучениеДанныхТТН

Функция прем_ТекстЗапросаТТНПоСвязанномуДокументу(Документ)
	
	ТекстЗапроса = Неопределено;
	
	ТипДокумента = ТипЗнч(Документ);
	
	Если ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
		Или ТипДокумента = Тип("ДокументСсылка.ПеремещениеТоваров")
		Или ТипДокумента = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		
		ТекстЗапроса = прем_ТекстЗапросаТТНПоОснованию();
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПриходныйОрдерНаТовары") Тогда
		
		ТекстЗапроса = прем_ТекстЗапросаТТНПоПриходномуОрдеру();
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция прем_ТекстЗапросаТТНПоОснованию()
	
	Возврат
	"ВЫБРАТЬ
	|	ТТНВходящаяЕГАИС.Ссылка КАК Документ
	|ПОМЕСТИТЬ НайденныеПоСвязанномуДокументуТТНВходящиеЕГАИС
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС КАК ТТНВходящаяЕГАИС
	|ГДЕ
	|	ТТНВходящаяЕГАИС.ДокументОснование = &СвязанныйДокумент";
	
КонецФункции

Функция прем_ТекстЗапросаТТНПоПриходномуОрдеру()
	
	Возврат
	"ВЫБРАТЬ
	|	ТТНВходящаяЕГАИС.Ссылка КАК Документ
	|ПОМЕСТИТЬ НайденныеПоСвязанномуДокументуТТНВходящиеЕГАИС
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС КАК ТТНВходящаяЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриходныйОрдерНаТовары КАК ПриходныйОрдерНаТовары
	|		ПО ТТНВходящаяЕГАИС.ДокументОснование = ПриходныйОрдерНаТовары.Распоряжение
	|			И (ПриходныйОрдерНаТовары.Ссылка = &СвязанныйДокумент)";
	
КонецФункции

Функция прем_ТекстЗапросаДанныхТТН(НеОтмененные = Ложь)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТТНВходящаяЕГАИС.Ссылка КАК Документ,
	|	ТТНВходящаяЕГАИС.Проведен КАК Проведен,
	|	ТТНВходящаяЕГАИС.ДатаТТН КАК ДатаТТН,
	|	ТТНВходящаяЕГАИС.НомерТТН КАК НомерТТН,
	|	СтатусыДокументовЕГАИС.Статус КАК ТекущийСтатус
	|ИЗ
	|	НайденныеПоСвязанномуДокументуТТНВходящиеЕГАИС КАК НайденныеПоСвязанномуДокументуТТНВходящиеЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТТНВходящаяЕГАИС КАК ТТНВходящаяЕГАИС
	|		ПО НайденныеПоСвязанномуДокументуТТНВходящиеЕГАИС.Документ = ТТНВходящаяЕГАИС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО НайденныеПоСвязанномуДокументуТТНВходящиеЕГАИС.Документ = СтатусыДокументовЕГАИС.Документ
	|ГДЕ
	|	НЕ ТТНВходящаяЕГАИС.ПометкаУдаления
	|	И &УсловиеФильтраПоСтатусу
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТТНВходящаяЕГАИС.Проведен УБЫВ,
	|	ТТНВходящаяЕГАИС.МоментВремени УБЫВ";
	
	Если НеОтмененные Тогда
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса,
			"&УсловиеФильтраПоСтатусу",
			"ВЫРАЗИТЬ(СтатусыДокументовЕГАИС.Статус КАК Перечисление.СтатусыОбработкиТТНВходящейЕГАИС) В (&Статусы)");
	Иначе
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса,
			"&УсловиеФильтраПоСтатусу",
			"Истина");
	КонецЕсли;
	
КонецФункции

#КонецОбласти // Конец ПолучениеДанныхТТН

#КонецОбласти
